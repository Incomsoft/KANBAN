
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.Отображение = ОтображениеТаблицы.Дерево; 
	Элементы.Список.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗадачу(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ЗадачаПользователяИерархияСсылка = СоздатьНовуюЗадачуПользователяИерархия(ТекущиеДанные);
	
	ПараметрыФормыСтруктура = Новый Структура;
	ПараметрыФормыСтруктура.Вставить("ЗадачаПользователяИерархия",ЗадачаПользователяИерархияСсылка);
	
	ОповещениеПриЗакрытии = Новый ОписаниеОповещения("СоздатьЗадачуЗавершение", ЭтотОбъект, ПараметрыФормыСтруктура);
	
	ОткрытьФорму("Задача.инкЗадачаПользователя.Форма.ФормаЗадачи",ПараметрыФормыСтруктура,,,,,ОповещениеПриЗакрытии);
	
КонецПроцедуры 

&НаКлиенте
Процедура СоздатьЗадачуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПроверкаЗадачиНаСоздвание(ДополнительныеПараметры.ЗадачаПользователяИерархия);
	ОбновитьСписок(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаЗадачиНаСоздвание(ЗадачаПользователяИерархияСсылка)
	
	Если НЕ ЗначениеЗаполнено(ЗадачаПользователяИерархияСсылка.ЗадачаПользователя) Тогда
		
		УдалитьЗадачуНаСервере(ЗадачаПользователяИерархияСсылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СоздатьНовуюЗадачуПользователяИерархия(ТекущиеДанные)
	
	ЗадачаРодитель = Неопределено;
	Если ТекущиеДанные <> Неопределено Тогда
		ЗадачаРодитель = ТекущиеДанные.Ссылка; 
	КонецЕсли;
	
	ЗадачаПользователяИерархияОбъект = Справочники.инкЗадачаПользователяИерархия.СоздатьЭлемент();
	ЗадачаПользователяИерархияОбъект.Родитель = ЗадачаРодитель;
	ЗадачаПользователяИерархияОбъект.Записать();
	
	Возврат ЗадачаПользователяИерархияОбъект.Ссылка;
	
КонецФункции

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;   
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат; 
	КонецЕсли;

	ПараметрыФормыСтруктура = Новый Структура;
	ПараметрыФормыСтруктура.Вставить("Ключ",ТекущиеДанные.ЗадачаПользователя);

	ОткрытьФорму("Задача.инкЗадачаПользователя.Форма.ФормаЗадачи",ПараметрыФормыСтруктура);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.инкЗадачаПользователяИерархия"));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачСтандартный(Команда)
	
	ОткрытьФорму("Задача.инкЗадачаПользователя.Форма.ФормаСпискаЗадач");
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЗадачу(Команда)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;  
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = "Удалить "+ТекущиеДанные.Ссылка+" задачу?";
	
	ПараметрыУдаления = Новый Структура("Ссылка",ТекущиеДанные.Ссылка);
	
    ПоказатьВопрос(
      Новый ОписаниеОповещения("УдалитьЗадачуЗавершение", ЭтотОбъект,ПараметрыУдаления),
      ТекстВопроса, 
      РежимДиалогаВопрос.ДаНет);

КонецПроцедуры 

&НаКлиенте
Процедура УдалитьЗадачуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
    Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		УдалитьЗадачуНаСервере(ДополнительныеПараметры.Ссылка);
		
	КонецЕсли;
	
	ОбновитьСписок(Неопределено);
	
КонецПроцедуры


&НаСервере
Процедура УдалитьЗадачуНаСервере(ЗадачаПользователяИерархияСсылка)
	
	Если НЕ ЗначениеЗаполнено(ЗадачаПользователяИерархияСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	// Очистка иерархии от задачи:
	Попытка

		ЗадачаПользователяИерархияОбъект = ЗадачаПользователяИерархияСсылка.ПолучитьОбъект(); 
		ЗадачаПользователяИерархияОбъект.Удалить();
		
	Исключение
		
		инкОбщийКлиентСервер.СообщитьПользователю("Ошибка при удалении. Описание: "+ОписаниеОшибки());
		
	КонецПопытки;
	
КонецПроцедуры

